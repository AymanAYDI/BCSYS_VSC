OBJECT Codeunit 1248 Process Bank Acc. Rec Lines
{
  OBJECT-PROPERTIES
  {
    Date=09/09/14;
    Time=12:00:00;
    Version List=NAVW18.00;
  }
  PROPERTIES
  {
    TableNo=274;
    Permissions=TableData 1220=rimd;
    OnRun=VAR
            PostingExch@1001 : Record 1220;
            ProcessPostingExch@1002 : Codeunit 1201;
            RecRef@1000 : RecordRef;
          BEGIN
            PostingExch.GET("Posting Exch. Entry No.");
            RecRef.GETTABLE(Rec);
            ProcessPostingExch.ProcessAllLinesColumnMapping(PostingExch,RecRef);
          END;

  }
  CODE
  {
    VAR
      ProgressWindowMsg@1000 : TextConst 'ENU=Please wait while the operation is being completed.;FRA=Veuillez patienter lors de l''ex‚cution de l''op‚ration.';

    PROCEDURE ImportBankStatement@6(BankAccRecon@1004 : Record 273;PostingExch@1003 : Record 1220) : Boolean;
    VAR
      BankAcc@1000 : Record 270;
      PostExchDef@1001 : Record 1222;
      PostExchMapping@1002 : Record 1224;
      PostExchLineDef@1005 : Record 1227;
      BankAccReconLine@1006 : Record 274;
      ProgressWindow@1008 : Dialog;
    BEGIN
      BankAcc.GET(BankAccRecon."Bank Account No.");
      BankAcc.GetPostExchDef(PostExchDef);

      IF NOT PostingExch.ImportToPostExch(PostExchDef)THEN
        EXIT(FALSE);

      ProgressWindow.OPEN(ProgressWindowMsg);

      CreateBankAccRecLineTemplate(BankAccReconLine,BankAccRecon,PostingExch);
      PostExchLineDef.SETRANGE("Posting Exch. Def Code",PostExchDef.Code);
      PostExchLineDef.FINDFIRST;

      PostExchMapping.GET(PostExchDef.Code,PostExchLineDef.Code,DATABASE::"Bank Acc. Reconciliation Line");

      IF PostExchMapping."Pre-Mapping Codeunit" <> 0 THEN
        CODEUNIT.RUN(PostExchMapping."Pre-Mapping Codeunit",BankAccReconLine);

      PostExchMapping.TESTFIELD("Mapping Codeunit");
      CODEUNIT.RUN(PostExchMapping."Mapping Codeunit",BankAccReconLine);

      IF PostExchMapping."Post-Mapping Codeunit" <> 0 THEN
        CODEUNIT.RUN(PostExchMapping."Post-Mapping Codeunit",BankAccReconLine);

      ProgressWindow.CLOSE;
      EXIT(TRUE);
    END;

    LOCAL PROCEDURE CreateBankAccRecLineTemplate@9(VAR BankAccReconLine@1000 : Record 274;BankAccRecon@1002 : Record 273;PostExch@1001 : Record 1220);
    BEGIN
      BankAccReconLine.INIT;
      BankAccReconLine."Statement Type" := BankAccRecon."Statement Type";
      BankAccReconLine."Statement No." := BankAccRecon."Statement No.";
      BankAccReconLine."Bank Account No." := BankAccRecon."Bank Account No.";
      BankAccReconLine."Posting Exch. Entry No." := PostExch."Entry No.";
    END;

    BEGIN
    END.
  }
}


OBJECT Codeunit 452 Report Distribution Management
{
  OBJECT-PROPERTIES
  {
    Date=09/09/14;
    Time=12:00:00;
    Version List=NAVW18.00;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      SaveFileDialogFilterMsg@1000 : TextConst 'ENU=PDF Files (*.pdf)|*.pdf;FRA=Fichiers PDF (*.pdf)|*.pdf';
      SaveFileDialogTitleMsg@1002 : TextConst 'ENU=Save PDF file;FRA=Enregistrer le fichier PDF';
      UnSupportedTableTypeErr@1003 : TextConst 'ENU=The table %1 is not supported.;FRA=La table %1 n''est pas prise en charge.';
      ServerSaveAsPdfFailedErr@1004 : TextConst 'ENU=An unexpected error occurred. Try the operation again or contact your system administrator.;FRA=Une erreur inattendue s''est produite. R‚essayez ou contactez votre administrateur systŠme.';
      ServerSaveAsPdfAbortedErr@1006 : TextConst 'ENU=You must select a sales invoice.;FRA=Vous devez s‚lectionner une facture vente.';
      AttachmentFileNameCap@1005 : TextConst '@@@="%1 = Company Name. %2 = Document Type %3 = Invoice No.";ENU=%1 - %2 %3.pdf;FRA=%1 - %2 %3.pdf';
      EmailSubjectCap@1007 : TextConst '@@@="%1 = Customer Name. %2 = Document Type %3 = Invoice No.";ENU=%1 - %2 %3;FRA=%1 - %2 %3';
      SendDocTo@1009 : 'Email,Disk';
      HideMailDialog@1008 : Boolean;

    LOCAL PROCEDURE GetAttachmentFileName@1000(DocumentNo@1003 : Code[20];DocumentType@1005 : Text) : Text;
    VAR
      FileMgt@1000 : Codeunit 419;
    BEGIN
      EXIT(STRSUBSTNO(AttachmentFileNameCap,FileMgt.StripNotsupportChrInFileName(COMPANYNAME),DocumentType,DocumentNo));
    END;

    LOCAL PROCEDURE GetToAddress@7(BillToCustomerNo@1000 : Code[20]) : Text;
    VAR
      Customer@1002 : Record 18;
      ToAddress@1001 : Text;
    BEGIN
      IF Customer.GET(BillToCustomerNo) THEN
        ToAddress := Customer."E-Mail";

      EXIT(ToAddress);
    END;

    LOCAL PROCEDURE GetEmailSubject@5(BillToName@1003 : Text;DocumentNo@1002 : Code[20];DocumentType@1001 : Text) : Text;
    BEGIN
      EXIT(STRSUBSTNO(EmailSubjectCap,BillToName,DocumentType,DocumentNo));
    END;

    LOCAL PROCEDURE SendSalesInvoiceReport@3(SalesInvHeader@1001 : Record 112;ReportID@1000 : Integer;SendTo@1004 : 'Email,Disk');
    VAR
      SalesHeader@1003 : Record 36;
      FileManagement@1010 : Codeunit 419;
      ServerAttachmentFilePath@1007 : Text;
      DocumentType@1002 : Text;
    BEGIN
      ServerAttachmentFilePath := FileManagement.ServerTempFileName('pdf');

      WITH SalesInvHeader DO BEGIN
        SETRECFILTER;
        IF NOT REPORT.SAVEASPDF(ReportID,ServerAttachmentFilePath,SalesInvHeader) THEN
          ERROR(ServerSaveAsPdfFailedErr);

        IF NOT EXISTS(ServerAttachmentFilePath) THEN
          ERROR(ServerSaveAsPdfAbortedErr);

        DocumentType := FORMAT(SalesHeader."Document Type"::Invoice);
        SendAttachment("No.","Bill-to Customer No.","Bill-to Name",ServerAttachmentFilePath,DocumentType,SendTo)
      END;
    END;

    PROCEDURE SendSalesCreditMemoReport@9(SalesCrMemoHeader@1001 : Record 114;ReportID@1000 : Integer;SendTo@1004 : 'Email,Disk');
    VAR
      SalesHeader@1003 : Record 36;
      FileManagement@1007 : Codeunit 419;
      ServerAttachmentFilePath@1006 : Text;
      DocumentType@1002 : Text;
    BEGIN
      ServerAttachmentFilePath := FileManagement.ServerTempFileName('pdf');

      WITH SalesCrMemoHeader DO BEGIN
        SETRECFILTER;
        IF NOT REPORT.SAVEASPDF(ReportID,ServerAttachmentFilePath,SalesCrMemoHeader) THEN
          ERROR(ServerSaveAsPdfFailedErr);

        IF NOT EXISTS(ServerAttachmentFilePath) THEN
          ERROR(ServerSaveAsPdfAbortedErr);

        DocumentType := FORMAT(SalesHeader."Document Type"::"Credit Memo");
        SendAttachment("No.","Bill-to Customer No.","Bill-to Name",ServerAttachmentFilePath,DocumentType,SendTo)
      END;
    END;

    PROCEDURE SendSalesQuoteReport@8(SalesHeader@1001 : Record 36;ReportID@1000 : Integer;SendTo@1003 : 'Email,Disk');
    VAR
      FileManagement@1007 : Codeunit 419;
      ServerAttachmentFilePath@1006 : Text;
      DocumentType@1002 : Text;
    BEGIN
      ServerAttachmentFilePath := FileManagement.ServerTempFileName('pdf');

      WITH SalesHeader DO BEGIN
        SETRECFILTER;
        IF NOT REPORT.SAVEASPDF(ReportID,ServerAttachmentFilePath,SalesHeader) THEN
          ERROR(ServerSaveAsPdfFailedErr);

        IF NOT EXISTS(ServerAttachmentFilePath) THEN
          ERROR(ServerSaveAsPdfAbortedErr);

        DocumentType := FORMAT("Document Type"::Quote);
        SendAttachment("No.","Bill-to Customer No.","Bill-to Name",ServerAttachmentFilePath,DocumentType,SendTo)
      END;
    END;

    PROCEDURE SaveDocumentReport@11(HeaderDoc@1000 : Variant);
    BEGIN
      HandleDocumentReport(HeaderDoc,SendDocTo::Disk);
    END;

    PROCEDURE EmailDocumentReport@1(HeaderDoc@1000 : Variant);
    BEGIN
      HandleDocumentReport(HeaderDoc,SendDocTo::Email);
    END;

    LOCAL PROCEDURE HandleDocumentReport@13(HeaderDoc@1000 : Variant;SendTo@1001 : 'Email,Disk');
    VAR
      ReportSelections@1006 : Record 77;
      SalesInvoiceHeader@1005 : Record 112;
      SalesCrMemoHeader@1004 : Record 114;
      SalesHeader@1003 : Record 36;
      SalesHeaderRecRef@1002 : RecordRef;
    BEGIN
      SalesHeaderRecRef.GETTABLE(HeaderDoc);

      CASE SalesHeaderRecRef.NUMBER OF
        DATABASE::"Sales Invoice Header":
          BEGIN
            ReportSelections.SETRANGE(Usage,ReportSelections.Usage::"S.Invoice");
            ReportSelections.FINDFIRST;
            SalesHeaderRecRef.SETTABLE(SalesInvoiceHeader);
            SendSalesInvoiceReport(SalesInvoiceHeader,ReportSelections."Report ID",SendTo)
          END;
        DATABASE::"Sales Cr.Memo Header":
          BEGIN
            ReportSelections.SETRANGE(Usage,ReportSelections.Usage::"S.Cr.Memo");
            ReportSelections.FINDFIRST;
            SalesHeaderRecRef.SETTABLE(SalesCrMemoHeader);
            SendSalesCreditMemoReport(SalesCrMemoHeader,ReportSelections."Report ID",SendTo);
          END;
        DATABASE::"Sales Header":
          BEGIN
            SalesHeaderRecRef.SETTABLE(SalesHeader);
            CASE SalesHeader."Document Type" OF
              SalesHeader."Document Type"::Quote:
                BEGIN
                  ReportSelections.SETRANGE(Usage,ReportSelections.Usage::"S.Quote");
                  ReportSelections.FINDFIRST;
                  SendSalesQuoteReport(HeaderDoc,ReportSelections."Report ID",SendTo);
                END;
              ELSE
                ERROR(STRSUBSTNO(UnSupportedTableTypeErr,FORMAT(SalesHeader."Document Type")));
            END;
          END;
        ELSE
          ERROR(STRSUBSTNO(UnSupportedTableTypeErr,SalesHeaderRecRef.NAME));
      END;
    END;

    PROCEDURE DownloadPdfOnClient@12(ServerPdfFilePath@1000 : Text) : Text;
    VAR
      FileManagement@1001 : Codeunit 419;
      ClientPdfFilePath@1002 : Text;
    BEGIN
      ClientPdfFilePath := FileManagement.DownloadTempFile(ServerPdfFilePath);
      ERASE(ServerPdfFilePath);
      EXIT(ClientPdfFilePath);
    END;

    LOCAL PROCEDURE SaveAsPdfOnClient@1002(ServerPdfFilePath@1000 : Text;AttachmentFilename@1005 : Text);
    VAR
      FileManagement@1004 : Codeunit 419;
    BEGIN
      FileManagement.DownloadHandler(ServerPdfFilePath,SaveFileDialogTitleMsg,'',SaveFileDialogFilterMsg,AttachmentFilename);
    END;

    LOCAL PROCEDURE SendAttachment@6(DocumentNo@1000 : Code[20];BillToCustomerNo@1001 : Code[20];BillToName@1003 : Text;ParmAttachmentFilePath@1002 : Text;DocumentType@1005 : Text;SendTo@1009 : 'Email,Disk');
    VAR
      TempEmailItem@1004 : TEMPORARY Record 9500;
      AttachmentFileName@1006 : Text;
    BEGIN
      AttachmentFileName := GetAttachmentFileName(DocumentNo,DocumentType);

      IF SendTo = SendTo::Disk THEN BEGIN
        SaveAsPdfOnClient(ParmAttachmentFilePath,AttachmentFileName);
        EXIT;
      END;

      WITH TempEmailItem DO BEGIN
        Init;
        "Send to" := COPYSTR(GetToAddress(BillToCustomerNo),1,MAXSTRLEN("Send to"));
        Subject := COPYSTR(GetEmailSubject(BillToName,DocumentNo,DocumentType),1,MAXSTRLEN(Subject));
        SetBodyText('');
        "Attachment File Path" := COPYSTR(ParmAttachmentFilePath,1,MAXSTRLEN("Attachment File Path"));
        "Attachment Name" := COPYSTR(AttachmentFileName,1,MAXSTRLEN("Attachment Name"));
        Send(HideMailDialog);
      END;
    END;

    PROCEDURE InitializeFrom@10(NewHideMailDialog@1001 : Boolean);
    BEGIN
      HideMailDialog := NewHideMailDialog;
    END;

    BEGIN
    END.
  }
}


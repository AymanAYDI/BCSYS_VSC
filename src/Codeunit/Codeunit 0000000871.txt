OBJECT Codeunit 871 Social Listening Management
{
  OBJECT-PROPERTIES
  {
    Date=07/01/15;
    Time=12:00:00;
    Version List=NAVW18.00.00.39368;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      MslProductNameTxt@1000 : TextConst '@@@={locked};ENU=Microsoft Social Listening;FRA=Microsoft Social Listening';
      FailedToConnectTxt@1002 : TextConst '@@@="%1 = Microsoft Social Listening, %2 = refresh";ENU=Failed to connect to %1.<br><br>Verify the configuration of %1 in Microsoft Dynamics NAV.<br><br>Afterwards %2 to try connecting to %1 again.;FRA=Impossible de se connecter … %1.<br><br>V‚rifiez la configuration de %1 dans Microsoft Dynamics NAV.<br><br>Cliquez ensuite sur %2 et tentez de vous reconnecter … %1.';
      HasNotBeenAuthenticatedTxt@1001 : TextConst '@@@="%1 = Microsoft Social Listening, %2= Microsoft Social Listening,%3 = refresh";ENU=%1 has not been authenticated.<br><br>Go to %2 to open the authentication window.<br><br>Afterwards %3 to show data.;FRA=%1 n''a pas ‚t‚ authentifi‚.<br><br>Acc‚dez … %2 pour ouvrir la fenˆtre d''authentification.<br><br>Cliquez ensuite sur %3 pour afficher les donn‚es.';
      ExpectedValueErr@1004 : TextConst 'ENU=Expected value should be an integer or url path containing %2 in %1.;FRA=La valeur attendue doit ˆtre un entier ou une url contenant %2 dans %1.';
      RefreshTxt@1003 : TextConst 'ENU=refresh;FRA=actualiser';

    PROCEDURE GetSignupURL@17() : Text[250];
    BEGIN
      EXIT('http://go.microsoft.com/fwlink/p/?LinkId=401462');
    END;

    PROCEDURE GetTermsOfUseURL@1() : Text[250];
    BEGIN
      EXIT('http://go.microsoft.com/fwlink/?LinkID=389042');
    END;

    PROCEDURE GetMSL_URL@5() : Text[250];
    VAR
      SocialListeningSetup@1000 : Record 870;
    BEGIN
      WITH SocialListeningSetup DO BEGIN
        IF GET AND ("Social Listening URL" <> '') THEN
          EXIT(COPYSTR("Social Listening URL",1,STRPOS("Social Listening URL",'/app/') - 1));
        TESTFIELD("Social Listening URL");
      END;
    END;

    PROCEDURE GetMSLAppURL@15() : Text[250];
    BEGIN
      EXIT(STRSUBSTNO('%1/app/%2/',GetMSL_URL,GetMSLSubscriptionID));
    END;

    PROCEDURE MSLUsersURL@16() : Text;
    BEGIN
      EXIT(STRSUBSTNO('%1/settings/%2/?locale=%3#page:users',GetMSL_URL,GetMSLSubscriptionID,GetLanguage));
    END;

    PROCEDURE MSLSearchItemsURL@2() : Text;
    BEGIN
      EXIT(STRSUBSTNO('%1/settings/%2/?locale=%3#page:searchitems',GetMSL_URL,GetMSLSubscriptionID,GetLanguage));
    END;

    LOCAL PROCEDURE MSLAuthenticationURL@3() : Text;
    BEGIN
      EXIT(STRSUBSTNO('%1/widgetapi/%2/authenticate.htm?lang=%3',GetMSL_URL,GetMSLSubscriptionID,GetLanguage));
    END;

    PROCEDURE MSLAuthenticationStatusURL@4() : Text;
    BEGIN
      EXIT(STRSUBSTNO('%1/widgetapi/%2/auth_status.htm?lang=%3',GetMSL_URL,GetMSLSubscriptionID,GetLanguage));
    END;

    PROCEDURE GetAuthenticationWidget@12(SearchTopic@1000 : Text) : Text;
    BEGIN
      EXIT(
        STRSUBSTNO(
          '%1/widgetapi/%2/?locale=%3#widgets:analytics_summary&nodeIds=%4',
          GetMSL_URL,GetMSLSubscriptionID,GetLanguage,SearchTopic));
    END;

    LOCAL PROCEDURE GetAuthenticationLink@6() : Text;
    BEGIN
      EXIT(
        STRSUBSTNO(
          '<a style="text-decoration: none" href="javascript:;" onclick="openAuthenticationWindow(''%1'');">%2</a>',
          MSLAuthenticationURL,MslProductNameTxt));
    END;

    LOCAL PROCEDURE GetRefreshLink@8() : Text;
    BEGIN
      EXIT(STRSUBSTNO('<a style="text-decoration: none" href="javascript:;" onclick="raiseMessageLinkClick(1);">%1</a>',RefreshTxt));
    END;

    LOCAL PROCEDURE GetMSLSubscriptionID@18() : Text[250];
    VAR
      SocialListeningSetup@1000 : Record 870;
    BEGIN
      SocialListeningSetup.GET;
      SocialListeningSetup.TESTFIELD("Solution ID");
      EXIT(SocialListeningSetup."Solution ID");
    END;

    LOCAL PROCEDURE GetLanguage@30() : Text;
    VAR
      CultureInfo@1000 : DotNet "'mscorlib, Version=4.0.0.0, Culture=neutral, PublicKeyToken=b77a5c561934e089'.System.Globalization.CultureInfo";
    BEGIN
      CultureInfo := CultureInfo.CultureInfo(GLOBALLANGUAGE);
      EXIT(CultureInfo.TwoLetterISOLanguageName);
    END;

    PROCEDURE GetAuthenticationConectionErrorMsg@10() : Text;
    BEGIN
      EXIT(STRSUBSTNO(FailedToConnectTxt,MslProductNameTxt,GetRefreshLink));
    END;

    PROCEDURE GetAuthenticationUserErrorMsg@11() : Text;
    BEGIN
      EXIT(STRSUBSTNO(HasNotBeenAuthenticatedTxt,MslProductNameTxt,GetAuthenticationLink,GetRefreshLink));
    END;

    PROCEDURE GetCustFactboxVisibility@7(Cust@1000 : Record 18;VAR MSLSetupVisibilty@1001 : Boolean;VAR MSLVisibility@1002 : Boolean);
    VAR
      SocialListeningSetup@1005 : Record 870;
      SocialListeningSearchTopic@1004 : Record 871;
    BEGIN
      WITH SocialListeningSetup DO
        MSLSetupVisibilty := GET AND "Show on Customers" AND "Accept License Agreement" AND ("Solution ID" <> '');

      WITH SocialListeningSearchTopic DO
        MSLVisibility := FindSearchTopic("Source Type"::Customer,Cust."No.") AND ("Search Topic" <> '') AND MSLSetupVisibilty;
    END;

    PROCEDURE GetVendFactboxVisibility@9(Vend@1000 : Record 23;VAR MSLSetupVisibilty@1001 : Boolean;VAR MSLVisibility@1002 : Boolean);
    VAR
      SocialListeningSetup@1005 : Record 870;
      SocialListeningSearchTopic@1004 : Record 871;
    BEGIN
      WITH SocialListeningSetup DO
        MSLSetupVisibilty := GET AND "Show on Vendors" AND "Accept License Agreement" AND ("Solution ID" <> '');

      WITH SocialListeningSearchTopic DO
        MSLVisibility := FindSearchTopic("Source Type"::Vendor,Vend."No.") AND ("Search Topic" <> '') AND MSLSetupVisibilty;
    END;

    PROCEDURE GetItemFactboxVisibility@14(Item@1000 : Record 27;VAR MSLSetupVisibilty@1001 : Boolean;VAR MSLVisibility@1002 : Boolean);
    VAR
      SocialListeningSetup@1005 : Record 870;
      SocialListeningSearchTopic@1004 : Record 871;
    BEGIN
      WITH SocialListeningSetup DO
        MSLSetupVisibilty := GET AND "Show on Items" AND "Accept License Agreement" AND ("Solution ID" <> '');

      WITH SocialListeningSearchTopic DO
        MSLVisibility := FindSearchTopic("Source Type"::Item,Item."No.") AND ("Search Topic" <> '') AND MSLSetupVisibilty;
    END;

    PROCEDURE ConvertURLToID@13(URL@1000 : Text;where@1006 : Text) : Text[250];
    VAR
      i@1001 : Integer;
      j@1003 : Integer;
      PositionOfID@1004 : Integer;
      ID@1002 : Text;
      IntegerValue@1005 : Integer;
    BEGIN
      IF URL = '' THEN
        EXIT(URL);
      IF EVALUATE(IntegerValue,URL) THEN
        EXIT(URL);

      PositionOfID := STRPOS(LOWERCASE(URL),LOWERCASE(where));
      IF PositionOfID = 0 THEN
        ERROR(ExpectedValueErr,where,URL);

      j := 1;
      i := PositionOfID + STRLEN(where);
      WHILE (URL[i] IN ['0'..'9']) AND (i <= STRLEN(URL)) DO BEGIN
        ID[j] := URL[i];

        i += 1;
        j += 1;
      END;

      IF ID = '' THEN
        ERROR(ExpectedValueErr,where,LOWERCASE(GetMSL_URL));
      EXIT(ID);
    END;

    PROCEDURE CheckURLPath@20(URL@1000 : Text;where@1001 : Text);
    VAR
      IntegerValue@1002 : Integer;
    BEGIN
      IF URL = '' THEN
        EXIT;
      IF EVALUATE(IntegerValue,URL) THEN
        EXIT;

      IF STRPOS(LOWERCASE(URL),LOWERCASE(GetMSL_URL)) = 0 THEN
        ERROR(ExpectedValueErr,where,LOWERCASE(GetMSL_URL));
    END;

    BEGIN
    END.
  }
}


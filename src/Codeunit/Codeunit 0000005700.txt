OBJECT Codeunit 5700 User Setup Management
{
  OBJECT-PROPERTIES
  {
    Date=17/10/16;
    Time=18:00:00;
    Modified=Yes;
    Version List=NAVW17.00,JX-VSC1.0-XAD;
  }
  PROPERTIES
  {
    Permissions=TableData 14=r,
                TableData 5714=r;
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=customer;FRA=client';
      Text001@1001 : TextConst 'ENU=vendor;FRA=fournisseur';
      Text002@1002 : TextConst 'ENU=This %1 is related to %2 %3. Your identification is setup to process from %2 %4.;FRA=Ce %1 est associ‚ au %2 %3. Votre identification est param‚tr‚e pour travailler … partir de %2 %4.';
      Text003@1003 : TextConst 'ENU=This document will be processed in your %2.;FRA=Ce document sera trait‚ dans votre %2.';
      UserSetup@1004 : Record 91;
      RespCenter@1005 : Record 5714;
      CompanyInfo@1006 : Record 79;
      UserLocation@1008 : Code[10];
      UserRespCenter@1014 : Code[10];
      SalesUserRespCenter@1013 : Code[10];
      PurchUserRespCenter@1012 : Code[10];
      ServUserRespCenter@1007 : Code[10];
      HasGotSalesUserSetup@1009 : Boolean;
      HasGotPurchUserSetup@1010 : Boolean;
      HasGotServUserSetup@1011 : Boolean;

    PROCEDURE GetSalesFilter@1() : Code[10];
    BEGIN
      EXIT(GetSalesFilter2(USERID));
    END;

    PROCEDURE GetPurchasesFilter@3() : Code[10];
    BEGIN
      EXIT(GetPurchasesFilter2(USERID));
    END;

    PROCEDURE GetServiceFilter@5() : Code[10];
    BEGIN
      EXIT(GetServiceFilter2(USERID));
    END;

    PROCEDURE GetSalesFilter2@6(UserCode@1000 : Code[50]) : Code[10];
    BEGIN
      IF NOT HasGotSalesUserSetup THEN BEGIN
        CompanyInfo.GET;
        SalesUserRespCenter := CompanyInfo."Responsibility Center";
        UserLocation := CompanyInfo."Location Code";
        IF UserSetup.GET(UserCode) AND (UserCode <> '') THEN
          IF UserSetup."Sales Resp. Ctr. Filter" <> '' THEN
            SalesUserRespCenter := UserSetup."Sales Resp. Ctr. Filter";
        HasGotSalesUserSetup := TRUE;
      END;
      EXIT(SalesUserRespCenter);
    END;

    PROCEDURE GetPurchasesFilter2@13(UserCode@1000 : Code[50]) : Code[10];
    BEGIN
      IF NOT HasGotPurchUserSetup THEN BEGIN
        CompanyInfo.GET;
        PurchUserRespCenter := CompanyInfo."Responsibility Center";
        UserLocation := CompanyInfo."Location Code";
        IF UserSetup.GET(UserCode) AND (UserCode <> '') THEN
          IF UserSetup."Purchase Resp. Ctr. Filter" <> '' THEN
            PurchUserRespCenter := UserSetup."Purchase Resp. Ctr. Filter";
        HasGotPurchUserSetup := TRUE;
      END;
      EXIT(PurchUserRespCenter);
    END;

    PROCEDURE GetServiceFilter2@17(UserCode@1000 : Code[50]) : Code[10];
    BEGIN
      IF NOT HasGotServUserSetup THEN BEGIN
        CompanyInfo.GET;
        ServUserRespCenter := CompanyInfo."Responsibility Center";
        UserLocation := CompanyInfo."Location Code";
        IF UserSetup.GET(UserCode) AND (UserCode <> '') THEN
          IF UserSetup."Service Resp. Ctr. Filter" <> '' THEN
            ServUserRespCenter := UserSetup."Service Resp. Ctr. Filter";
        HasGotServUserSetup := TRUE;
      END;
      EXIT(ServUserRespCenter);
    END;

    PROCEDURE GetRespCenter@2(DocType@1000 : 'Sales,Purchase,Service';AccRespCenter@1001 : Code[10]) : Code[10];
    VAR
      AccType@1002 : Text[50];
    BEGIN
      CASE DocType OF
        DocType::Sales:
          BEGIN
            AccType := Text000;
            UserRespCenter := GetSalesFilter;
          END;
        DocType::Purchase:
          BEGIN
            AccType := Text001;
            UserRespCenter := GetPurchasesFilter;
          END;
        DocType::Service:
          BEGIN
            AccType := Text000;
            UserRespCenter := GetServiceFilter;
          END;
      END;
      IF (AccRespCenter <> '') AND
         (UserRespCenter <> '') AND
         (AccRespCenter <> UserRespCenter)
      THEN
        MESSAGE(
          Text002 +
          Text003,
          AccType,RespCenter.TABLECAPTION,AccRespCenter,UserRespCenter);
      IF UserRespCenter = '' THEN
        EXIT(AccRespCenter);

      EXIT(UserRespCenter);
    END;

    PROCEDURE CheckRespCenter@4(DocType@1000 : 'Sales,Purchase,Service';AccRespCenter@1001 : Code[10]) : Boolean;
    BEGIN
      EXIT(CheckRespCenter2(DocType,AccRespCenter,USERID));
    END;

    PROCEDURE CheckRespCenter2@7(DocType@1000 : 'Sales,Purchase,Service';AccRespCenter@1001 : Code[20];UserCode@1002 : Code[50]) : Boolean;
    BEGIN
      CASE DocType OF
        DocType::Sales:
          UserRespCenter := GetSalesFilter2(UserCode);
        DocType::Purchase:
          UserRespCenter := GetPurchasesFilter2(UserCode);
        DocType::Service:
          UserRespCenter := GetServiceFilter2(UserCode);
      END;
      IF (UserRespCenter <> '') AND
         (AccRespCenter <> UserRespCenter)
      THEN
        EXIT(FALSE);

      EXIT(TRUE);
    END;

    PROCEDURE GetLocation@46(DocType@1000 : 'Sales,Purchase,Service';AccLocation@1001 : Code[10];RespCenterCode@1002 : Code[10]) : Code[10];
    BEGIN
      CASE DocType OF
        DocType::Sales:
          UserRespCenter := GetSalesFilter;
        DocType::Purchase:
          UserRespCenter := GetPurchasesFilter;
        DocType::Service:
          UserRespCenter := GetServiceFilter;
      END;
      IF UserRespCenter <> '' THEN
        RespCenterCode := UserRespCenter;
      IF RespCenter.GET(RespCenterCode) THEN
        IF RespCenter."Location Code" <> '' THEN
          UserLocation := RespCenter."Location Code";
      IF AccLocation <> '' THEN
        EXIT(AccLocation);

      EXIT(UserLocation);
    END;

    PROCEDURE jx_GetPurchasesFilter@1000000000() Ltxt_UserList : Text;
    VAR
      Lrec_UserSetup@1000000000 : Record 91;
      Lcode_CodeService@1000000001 : Code[20];
      Ltext_UserList@1000000002 : Text;
    BEGIN
      //Ajout JX-XAD du 13/06/2008
      //Filtrer les documents d'achats sur les utilisateurs dont les droits sont indiqu‚s dans les paramŠtres utilisateurs
      IF Lrec_UserSetup.GET(USERID) THEN
      BEGIN
        CASE Lrec_UserSetup."Droits accŠs doc. achat" OF
          Lrec_UserSetup."Droits accŠs doc. achat"::Utilisateur:
            Ltext_UserList := USERID;
          Lrec_UserSetup."Droits accŠs doc. achat"::Responsable:
          BEGIN
            Lcode_CodeService := Lrec_UserSetup.Service;
            Ltext_UserList := '';
            Lrec_UserSetup.RESET;
            Lrec_UserSetup.SETFILTER(Lrec_UserSetup.Service,COPYSTR(Lcode_CodeService,1,3)+'*');
            IF Lrec_UserSetup.FIND('-') THEN
            BEGIN
              REPEAT
                IF Ltext_UserList <> '' THEN
                  Ltext_UserList += '|' + Lrec_UserSetup."User ID"
                ELSE
                  Ltext_UserList += Lrec_UserSetup."User ID";
              UNTIL Lrec_UserSetup.NEXT = 0;
            END;
          END;
          Lrec_UserSetup."Droits accŠs doc. achat"::"Sous-Responsable":
          BEGIN
            Lcode_CodeService := Lrec_UserSetup.Service;
            Ltext_UserList := '';
            Lrec_UserSetup.RESET;
            Lrec_UserSetup.SETFILTER(Lrec_UserSetup.Service,Lcode_CodeService);
            IF Lrec_UserSetup.FIND('-') THEN
            BEGIN
              REPEAT
                IF Ltext_UserList <> '' THEN
                  Ltext_UserList += '|' + Lrec_UserSetup."User ID"
                ELSE
                  Ltext_UserList += Lrec_UserSetup."User ID";
              UNTIL Lrec_UserSetup.NEXT = 0;
            END;
          END;
          Lrec_UserSetup."Droits accŠs doc. achat"::Admin:
            Ltext_UserList := '';
        END;
      END ELSE
        Ltext_UserList := USERID;

      EXIT(Ltext_UserList);
    END;

    BEGIN
    {

      Ajout JX-XAD du 13/06/2008
      Cr‚ation d'une fonction jx_GetPurchasesFilter
      But : Filtrer les documents d'achats sur les utilisateurs dont les droits sont indiqu‚s dans les paramŠtres utilisateurs
    }
    END.
  }
}


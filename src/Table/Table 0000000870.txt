OBJECT Table 870 Social Listening Setup
{
  OBJECT-PROPERTIES
  {
    Date=07/01/15;
    Time=12:00:00;
    Version List=NAVW18.00.00.39368;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               IF "Signup URL" = '' THEN
                 "Signup URL" := SocialListeningMgt.GetSignupURL;
               IF "Terms of Use URL" = '' THEN
                 "Terms of Use URL" := SocialListeningMgt.GetTermsOfUseURL;
             END;

    CaptionML=[ENU=Social Listening Setup;
               FRA=Configuration de Social Listening];
  }
  FIELDS
  {
    { 1   ;   ;Primary Key         ;Code10        ;CaptionML=[ENU=Primary Key;
                                                              FRA=Cl‚ primaire] }
    { 2   ;   ;Solution ID         ;Text250       ;OnValidate=BEGIN
                                                                IF "Solution ID" = '' THEN BEGIN
                                                                  VALIDATE("Show on Items",FALSE);
                                                                  VALIDATE("Show on Customers",FALSE);
                                                                  VALIDATE("Show on Vendors",FALSE);
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Solution ID;
                                                              FRA=ID de la solution];
                                                   Editable=No }
    { 3   ;   ;Show on Items       ;Boolean       ;OnValidate=BEGIN
                                                                IF "Show on Items" THEN BEGIN
                                                                  TESTFIELD("Social Listening URL");
                                                                  TESTFIELD("Accept License Agreement");
                                                                  TESTFIELD("Solution ID");
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Show on Items;
                                                              FRA=Afficher sur les articles] }
    { 4   ;   ;Show on Customers   ;Boolean       ;OnValidate=BEGIN
                                                                IF "Show on Customers" THEN BEGIN
                                                                  TESTFIELD("Social Listening URL");
                                                                  TESTFIELD("Accept License Agreement");
                                                                  TESTFIELD("Solution ID");
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Show on Customers;
                                                              FRA=Afficher sur les clients] }
    { 5   ;   ;Show on Vendors     ;Boolean       ;OnValidate=BEGIN
                                                                IF "Show on Vendors" THEN BEGIN
                                                                  TESTFIELD("Social Listening URL");
                                                                  TESTFIELD("Accept License Agreement");
                                                                  TESTFIELD("Solution ID");
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Show on Vendors;
                                                              FRA=Afficher sur les fournisseurs] }
    { 6   ;   ;Accept License Agreement;Boolean   ;OnValidate=BEGIN
                                                                IF NOT "Accept License Agreement" THEN BEGIN
                                                                  VALIDATE("Show on Items",FALSE);
                                                                  VALIDATE("Show on Customers",FALSE);
                                                                  VALIDATE("Show on Vendors",FALSE);
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Accept License Agreement;
                                                              FRA=Accepter contrat de licence] }
    { 7   ;   ;Terms of Use URL    ;Text250       ;ExtendedDatatype=URL;
                                                   CaptionML=[ENU=Terms of Use URL;
                                                              FRA=URL Conditions d'utilisation];
                                                   NotBlank=Yes }
    { 8   ;   ;Signup URL          ;Text250       ;ExtendedDatatype=URL;
                                                   CaptionML=[ENU=Signup URL;
                                                              FRA=URL inscription];
                                                   NotBlank=Yes }
    { 9   ;   ;Social Listening URL;Text250       ;OnValidate=BEGIN
                                                                IF "Social Listening URL" = '' THEN BEGIN
                                                                  VALIDATE("Solution ID",'');
                                                                  EXIT;
                                                                END;

                                                                IF STRPOS("Social Listening URL",'https://') <> 1 THEN
                                                                  ERROR(MustStartWithErr,FIELDCAPTION("Social Listening URL"));
                                                                IF STRPOS("Social Listening URL",'/app/') = 0 THEN
                                                                  ERROR(MustContainSolutionIDErr,FIELDCAPTION("Social Listening URL"));

                                                                VALIDATE("Solution ID",SocialListeningMgt.ConvertURLToID("Social Listening URL",'/app/'));
                                                              END;

                                                   ExtendedDatatype=URL;
                                                   CaptionML=[ENU=Social Listening URL;
                                                              FRA=URL Social Listening] }
  }
  KEYS
  {
    {    ;Primary Key                             ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      SocialListeningMgt@1000 : Codeunit 871;
      MustStartWithErr@1001 : TextConst 'ENU=The %1 must start with ''https://''.;FRA=L''%1 doit commencer par ®ÿhttps://ÿ¯.';
      MustContainSolutionIDErr@1002 : TextConst 'ENU=The %1 must contain the Solution ID.;FRA=Le %1 doit contenir l''ID de solution.';

    BEGIN
    END.
  }
}

